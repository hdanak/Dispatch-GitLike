#!/usr/bin/env perl
use Modern::Perl;
use File::Basename;

my $def_cmd= 'help';
my $base = ($0 and $0 ne '-') ? basename($0)
                              : die "Multi-exec command called with no basename\n";
my @path = (split ':', $ENV{PATH});
my $cmd = (@ARGV and $ARGV[0] !~ /^-/) ? shift @ARGV : $def_cmd;

my %alias = (
    help   => sub {
        print "Possible commands:\n", map {"\t$_\n"} find_subcmds();
    },
);

if (exists $alias{$cmd}) {
    if ('CODE' eq ref($alias{$cmd})) {
        exit($alias{$cmd}->(@ARGV))
    } else {
        exec $alias{$cmd}, @ARGV;
    }
} else {
    # find the best exec candidate, even with a file extension
    # fall back to "$0-$cmd", which may be a relative path
    for (@path) {
        opendir(my $dh, $_) or next;
        my @files = sort grep {/^$base-$cmd\.?/} readdir $dh;
        closedir $dh;
        exec ("$_/$files[0]", @ARGV) if @files
    }
    exec "$0-$cmd", @ARGV;
}
sub find_subcmds {
    (
        keys(%alias),
        map { m|/$base-([^/]+)$| } map { glob("$_/$base-*") } @path
    )
}
